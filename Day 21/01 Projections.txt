@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Travel Root entity for bo'
@Metadata.ignorePropagatedAnnotations: true
define root view entity ZDXC_AB_M_TRAVEL as select from /dmo/travel_m
composition of exact one to many ZDXC_AB_M_BOOKING as _Bookings
association[1] to /DMO/I_Agency as _Agency on
$projection.AgencyId = _Agency.AgencyID
association[1] to /DMO/I_Customer as _Customer on
$projection.CustomerId = _Customer.CustomerID
association[1] to /DMO/I_Overall_Status_VH as _Status on
$projection.OverallStatus = _Status.OverallStatus
association[1] to I_Currency as _Currency on
$projection.CurrencyCode = _Currency.Currency
{
    @ObjectModel.text.element: [ 'Description' ]
    key travel_id as TravelId,
    @ObjectModel.text.element: [ 'AgencyName' ]
    @Consumption.valueHelpDefinition: [{ 
            entity.name: '/DMO/I_Agency',
            entity.element: 'AgencyID'
     }]
    agency_id as AgencyId,
    @Semantics.text: true
    _Agency.Name as AgencyName,
    @ObjectModel.text.element: [ 'CustomerName' ]
    @Consumption.valueHelpDefinition: [{ 
            entity.name: '/DMO/I_Customer',
            entity.element: 'CustomerID'
     }]
    customer_id as CustomerId,
    @Semantics.text: true
    _Customer.LastName as CustomerName,
    begin_date as BeginDate,
    end_date as EndDate,
    @Semantics.amount.currencyCode: 'CurrencyCode'
    booking_fee as BookingFee,
    @Semantics.amount.currencyCode: 'CurrencyCode'
    total_price as TotalPrice,
    currency_code as CurrencyCode,
    @Semantics.text: true
    description as Description,
    @Consumption.valueHelpDefinition: [{ 
            entity.name: '/DMO/I_Overall_Status_VH',
            entity.element: 'OverallStatus'
     }]
    @ObjectModel.text.element: [ 'StatusText' ] 
    overall_status as OverallStatus,
    @Semantics.text: true
    _Status._Text[Language = $session.system_language].Text as StatusText,
    case overall_status
        when 'A' then 3
        when 'O' then 2
        else 1 end as Criticality,
    created_by as CreatedBy,
    created_at as CreatedAt,
    last_changed_by as LastChangedBy,
    last_changed_at as LastChangedAt,
     _Agency,
     _Customer,
     _Status,
     _Currency,
     _Bookings
    --_association_name // Make association public
}




----------------------------------------------------------------------------------------------------------------
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'booking child entity'
@Metadata.ignorePropagatedAnnotations: true
define view entity ZDXC_AB_M_BOOKING as select from /dmo/booking_m
composition of exact one to many  ZDXC_AB_M_BOOKING_SUPPL as _Supplement
association to parent ZDXC_AB_M_TRAVEL as _Travel on
$projection.TravelId = _Travel.TravelId
association[1] to /DMO/I_Customer as _Customer on
$projection.CustomerId = _Customer.CustomerID
association[1] to /DMO/I_Carrier as _Carrier on
$projection.CarrierId = _Carrier.AirlineID
association[1] to /DMO/I_Connection as _Connection on
$projection.CarrierId = _Connection.AirlineID and
$projection.ConnectionId = _Connection.ConnectionID 
association[1] to /DMO/I_Flight as _Flight on
$projection.CarrierId = _Flight.AirlineID and
$projection.ConnectionId = _Flight.ConnectionID and
$projection.FlightDate = _Flight.FlightDate
{
    key travel_id as TravelId,
    key booking_id as BookingId,
    booking_date as BookingDate,
    customer_id as CustomerId,
    carrier_id as CarrierId,
    connection_id as ConnectionId,
    flight_date as FlightDate,
    @Semantics.amount.currencyCode: 'CurrencyCode'
    flight_price as FlightPrice,
    currency_code as CurrencyCode,
    booking_status as BookingStatus,
    last_changed_at as LastChangedAt,
    _Customer,
    _Carrier,
    _Connection,
    _Flight,
    _Travel,
    _Supplement
    --_association_name // Make association public
}

----------------------------------------------------------------------------------------------------------------
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Booking Supplement child (lowest)'
@Metadata.ignorePropagatedAnnotations: true
define view entity ZDXC_AB_M_BOOKING_SUPPL as select from /dmo/booksuppl_m
association to ZDXC_AB_M_TRAVEL as _Travel
on $projection.TravelId = _Travel.TravelId 
association to parent ZDXC_AB_M_BOOKING as _Booking
on $projection.TravelId = _Booking.TravelId and
    $projection.BookingId = _Booking.BookingId
association to /DMO/I_Supplement as _Supplements
on $projection.SupplementId = _Supplements.SupplementID    
--composition of target_data_source_name as _association_name
{
  key travel_id as TravelId,
  key booking_id as BookingId,
  key booking_supplement_id as BookingSupplementId,
  supplement_id as SupplementId,
  @Semantics.amount.currencyCode: 'CurrencyCode'
  price as Price,
  currency_code as CurrencyCode,
  last_changed_at as LastChangedAt,
  _Travel,
  _Booking,
  _Supplements
}

----------------------------------------------------------------------------------------------------------------
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Projection on travel entity for processor'
@Metadata.ignorePropagatedAnnotations: false
@Metadata.allowExtensions: true
define root view entity ZDXC_AB_M_TRAVEL_PROCESSOR
  as projection on ZDXC_AB_M_TRAVEL
{
    key TravelId,
    AgencyId,
    AgencyName,
    CustomerId,
    CustomerName,
    BeginDate,
    EndDate,
    BookingFee,
    TotalPrice,
    CurrencyCode,
    Description,
    OverallStatus,
    StatusText,
    Criticality,
    CreatedBy,
    CreatedAt,
    LastChangedBy,
    LastChangedAt,
    /* Associations */
    _Agency,
    _Bookings : redirected to composition child ZDXC_AB_M_BOOKING_PROCESSOR,
    _Currency,
    _Customer,
    _Status
}

----------------------------------------------------------------------------------------------------------------
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Booking processor projection'
@Metadata.ignorePropagatedAnnotations: false
@Metadata.allowExtensions: true
define view entity ZDXC_AB_M_BOOKING_PROCESSOR
  as projection on ZDXC_AB_M_BOOKING
{
    key TravelId,
    key BookingId,
    BookingDate,
    CustomerId,
    CarrierId,
    ConnectionId,
    FlightDate,
    FlightPrice,
    CurrencyCode,
    BookingStatus,
    LastChangedAt,
    /* Associations */
    _Carrier,
    _Connection,
    _Customer,
    _Flight,
    _Supplement: redirected to composition child ZDXC_AB_M_BOOKSUPPL_PROCESSOR,
    _Travel: redirected to parent ZDXC_AB_M_TRAVEL_PROCESSOR
}

----------------------------------------------------------------------------------------------------------------
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Booking Supplement projection layer'
@Metadata.ignorePropagatedAnnotations: false
define view entity ZDXC_AB_M_BOOKSUPPL_PROCESSOR
  as projection on ZDXC_AB_M_BOOKING_SUPPL
{
    key TravelId,
    key BookingId,
    key BookingSupplementId,
    SupplementId,
    Price,
    CurrencyCode,
    LastChangedAt,
    /* Associations */
    _Booking: redirected to parent ZDXC_AB_M_BOOKING_PROCESSOR,
    _Travel: redirected to ZDXC_AB_M_TRAVEL_PROCESSOR,
    _Supplements
}

----------------------------------------------------------------------------------------------------------------
@Metadata.layer: #CUSTOMER
@UI.headerInfo:{ 
    typeName: 'Travel',
    typeNamePlural: 'Travels',
    title: { value: 'TravelId' },
    description: { value: 'Description' }
}
annotate entity ZDXC_AB_M_TRAVEL_PROCESSOR
    with 
{
    @UI.facet: [
        { 
            type: #DATAPOINT_REFERENCE,
            purpose: #HEADER,
            id: 'Status',
            qualifier: 'Status'
        },
        { 
            type: #DATAPOINT_REFERENCE,
            purpose: #HEADER,
            id: 'Price',
            qualifier: 'Price'
        },{ 
            purpose: #STANDARD,
            type: #COLLECTION,
            id: 'mainBlock',
            label: 'General Information',
            position: 10        
        },
        { 
            purpose: #STANDARD,
            type: #IDENTIFICATION_REFERENCE,
            parentId: 'mainBlock',
            position: 10,
            label: 'Travel Basic'
        },
        { 
            purpose: #STANDARD,
            type: #FIELDGROUP_REFERENCE,
            parentId: 'mainBlock',
            position: 20,
            label: 'Dates',
            targetQualifier: 'dates'        
        },
        { 
            purpose: #STANDARD,
            type: #FIELDGROUP_REFERENCE,
            parentId: 'mainBlock',
            position: 30,
            label: 'Pricing',
            targetQualifier: 'prices'        
        },
        { 
            purpose: #STANDARD,
            type: #LINEITEM_REFERENCE,
            position: 20,
            label: 'Bookings',
            targetElement: '_Bookings'        
        }
    
    ]
    
    
    @UI.selectionField: [{ position: 10 }]
    @UI.lineItem: [{ position: 10 }]
    @UI.identification: [{ position: 10 }]
    TravelId;
    @UI.selectionField: [{ position: 20 }]
    @UI.lineItem: [{ position: 20 }]
    @UI.identification: [{ position: 20 }]
    AgencyId;
    @UI.selectionField: [{ position: 30 }]
    @UI.lineItem: [{ position: 30 }]
    @UI.identification: [{ position: 30 }]
    CustomerId;
    @UI.selectionField: [{ position: 40 }]
    @UI.fieldGroup: [{ position: 10, qualifier: 'dates' }]
    BeginDate;
    @UI.fieldGroup: [{ position: 20, qualifier: 'dates' }]
    EndDate;
    @UI.fieldGroup: [{ position: 10, qualifier: 'prices' }]
    BookingFee;
    @UI.selectionField: [{ position: 50 }]
    @UI.lineItem: [{ position: 40 }]    
    @UI.dataPoint: { qualifier: 'Price' }
    @UI.fieldGroup: [{ position: 20, qualifier: 'prices' }]
    TotalPrice;
    @UI.selectionField: [{ position: 60 }]
    @UI.fieldGroup: [{ position: 30, qualifier: 'prices' }]
    CurrencyCode;
//    Description;
    @UI.selectionField: [{ position: 70 }]
    @UI.lineItem: [{ position: 50, criticality: 'Criticality' }]
    @UI.dataPoint: { criticality: 'Criticality', qualifier: 'Status' }
    @UI.fieldGroup: [{ position: 30, qualifier: 'dates' }]
    OverallStatus;
//    CreatedBy;
//    CreatedAt;
//    LastChangedBy;
//    LastChangedAt;
    
}
----------------------------------------------------------------------------------------------------------------
@Metadata.layer: #CUSTOMER
@UI.headerInfo:{ 
    typeName: 'Book',
    typeNamePlural: 'Bookings',
    title: { value: 'BookingId' },
    description: { value: 'CarrierId' }
}
annotate entity ZDXC_AB_M_BOOKING_PROCESSOR
    with 
{
   --TravelId;
   @UI.lineItem: [{ position: 10 }]
   BookingId;
   @UI.lineItem: [{ position: 20 }]
   BookingDate;
   @UI.lineItem: [{ position: 30 }]
   CustomerId;
   @UI.lineItem: [{ position: 40 }]
   CarrierId;
   @UI.lineItem: [{ position: 50 }]
   ConnectionId;
   @UI.lineItem: [{ position: 60 }]
   FlightDate;
   @UI.lineItem: [{ position: 70 }]
   FlightPrice;
   --CurrencyCode;
   --BookingStatus;
   --LastChangedAt;
    
}
----------------------------------------------------------------------------------------------------------------



